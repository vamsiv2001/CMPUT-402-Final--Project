plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.15.0'
    id("net.ltgt.errorprone") version "latest.release"
}

group 'Tartan'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'application'

mainClassName = 'tartan.smarthome.TartanHomeApplication'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // JAX-B dependencies for JDK 9+
    implementation "javax.xml.bind:jaxb-api:2.2.11"
    implementation "com.sun.xml.bind:jaxb-core:2.2.11"
    implementation "com.sun.xml.bind:jaxb-impl:2.2.11"
    implementation "javax.activation:activation:1.1.1"
    implementation 'io.dropwizard:dropwizard-bom:1.0.0'
    implementation 'io.dropwizard:dropwizard-core:1.0.0'
    implementation 'io.dropwizard:dropwizard-views-freemarker:1.0.0'
    implementation 'io.dropwizard:dropwizard-auth:1.0.0'
    implementation 'io.dropwizard:dropwizard-hibernate:1.0.0'
    implementation 'io.dropwizard:dropwizard-assets:0.9.1'
    implementation 'mysql:mysql-connector-java:8.0.13'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    errorprone("com.google.errorprone:error_prone_core:latest.release")
}

run {
    args = ['server', 'config.yml']
}

shadowJar {
    mergeServiceFiles()
    manifest {
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': rootProject.version
        attributes 'Implementation-Vendor-Id': rootProject.group
        attributes 'Created-By': 'Gradle ' + gradle.gradleVersion
        attributes 'Main-Class': mainClassName
    }
    archiveFileName = "${rootProject.name}-${rootProject.version}.jar"
}

test {
    useJUnitPlatform()
}

pitest {
    junit5PluginVersion = '1.1.1'
    targetClasses = ['**tartan.smarthome.resources.StaticTartanStateEvaluator**']
    targetTests = [ 'tartan.smarthome.resources.IntruderDefense',
                    'tartan.smarthome.resources.KeylessEntry',
                    'tartan.smarthome.resources.NightLock',
                    'tartan.smarthome.resources.IntegrationTests',
                    'tartan.smarthome.resources.ElectronicOperation',
                    'tartan.smarthome.resources.R1',
                    'tartan.smarthome.resources.R3',
                    'tartan.smarthome.resources.R9',
                    'tartan.smarthome.resources.R12']
    outputFormats = ['HTML']
    timestampedReports = true
}

jacoco {
    toolVersion = "0.8.9"
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = true
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, includes: [
                '**/tartan/smarthome/resources/StaticTartanStateEvaluator.class', 
            ])
        }))
    }
}
